apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = "io.innerloop"
archivesBaseName = "spring-neo4j-ogm"
version = "0.2.0"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.springframework', name: 'spring-test', version: '4.2.4.RELEASE'
    testCompile group: 'org.neo4j.app', name: 'neo4j-server', version: '2.3.1'
    testCompile group: 'org.neo4j', name: 'neo4j-io', version: '2.3.1', classifier: 'tests'
    testCompile group: 'org.neo4j.app', name: 'neo4j-server', version: '2.3.1', classifier: 'tests'
    testCompile group: 'org.neo4j', name: 'neo4j-kernel', version: '2.3.1', classifier: 'tests'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'org.springframework', name: 'spring-context', version:'4.2.4.RELEASE'
    compile group: 'org.springframework', name: 'spring-tx', version:'4.2.4.RELEASE'
    compile group: 'io.innerloop', name:'java-neo4j-ogm', version: '0.3.0'
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Spring Neo4J OGM'
                packaging 'jar'
                description 'Spring integration for Java Neo4J Object Graph Mapper (OGM)'
                url 'https://github.com/inner-loop/spring-neo4j-ogm'

                scm {
                    connection 'git@github.com:inner-loop/spring-neo4j-ogm.git'
                    developerConnection 'git@github.com:inner-loop/spring-neo4j-ogm.git'
                    url 'https://github.com/inner-loop/spring-neo4j-ogm'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE, Version 2.0'
                        url 'http://www.gnu.org/licenses/gpl-2.0.html'
                    }
                }

                developers {
                    developer {
                        id 'mangrish'
                        name 'Mark Angrish'
                        email 'mark.angrish@innerloop.io'
                    }
                }
            }
        }
    }
}

